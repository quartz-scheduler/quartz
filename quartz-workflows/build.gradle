plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        getByName('maven').pom.description = 'Quartz Enterprise Job Scheduler'
    }
}

dependencies {
    implementation project(':quartz')

    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    runtimeOnly "org.slf4j:slf4j-log4j12:$slf4jVersion"


    testImplementation "junit:junit:$junitVersion"
    testImplementation 'org.mockito:mockito-core:2.22.0'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.slf4j:slf4j-simple:1.7.25'
}

test {
    maxParallelForks 1
    forkEvery 1
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options {
        docTitle = "Quartz Enterprise Job Scheduler $project.version API"
        encoding = 'UTF-8'
        outputLevel = JavadocOutputLevel.QUIET
        splitIndex = false
        author = true;
        windowTitle = "Quartz Enterprise Job Scheduler $project.version API"
        bottom = 'Copyright 2001-2023, Terracotta, Inc.'
    }
}


processResources {
    filesMatching('**/quartz-build.properties') {
        expand([version: project.version, fullname: project.fullname, name: project.name])
    }
}
